<?xml version="1.0" encoding="UTF-8" standalone="yes"?>
<manialink name="ESC:CountDown" id="CountDown" version="3">
    <frame pos="159 -5">
        <label id="cd-label" textprefix="$s" scale="0.7" textcolor="fff" style="TextRaceMessageBig" halign="right" />
    </frame>

    <script><!--
    #Include "MathLib" as ML
    #Include "TextLib" as TL

    main(){
        declare cdLabel <=> (Page.MainFrame.GetFirstChild("cd-label") as CMlLabel);
        declare Integer roundLimit = {{ esc\Controllers\MapController::getTimeLimit() + esc\Controllers\MapController::getAddedTime() }} * 60;
        declare Integer bestTime = {{  bestTime }};
        declare Text lastUpdateHash = "";
        declare status = 0;

        while(True){
            yield;

            if(InputPlayer == Null){
                wait(InputPlayer != Null);
            }

            declare TL_Hash for InputPlayer = "";
            declare TL_Limit for InputPlayer = {{ esc\Controllers\MapController::getTimeLimit() + esc\Controllers\MapController::getAddedTime() }};
            if(TL_Hash != lastUpdateHash){
                lastUpdateHash = TL_Hash;
                roundLimit = TL_Limit;
            }

            declare secondsPlayed = InputPlayer.CurRace.Time;

            if(InputPlayer != Null && secondsPlayed != -1){
                declare timeLeft = roundLimit * 1000 - secondsPlayed;

                declare minutesLeft = timeLeft / 60000;
                declare secondsLeft = (timeLeft - minutesLeft * 60000) / 1000;

                if(secondsLeft < 10){
                    cdLabel.SetText(minutesLeft ^ ":0" ^ secondsLeft);
                }else{
                    cdLabel.SetText(minutesLeft ^ ":" ^ secondsLeft);
                }

                if(timeLeft >= bestTime && (status == 1 || status == 2)){
                    status = 0;
                    cdLabel.TextColor = <1.0, 1.0, 1.0>;
                }

                if(timeLeft < bestTime && status == 0){
                    status = 1;
                    cdLabel.TextColor = <1.0, 0.75, 0.0>;
                }

                if(timeLeft < 10000 && status == 1){
                    status = 2;
                    cdLabel.TextColor = <1.0, 0.0, 0.0>;
                }
            }else{
                if(status != 0){
                    status = 0;
                    cdLabel.TextColor = <1.0, 1.0, 1.0>;
                    cdLabel.SetText("");
                }
            }
        }
    }

    --></script>
</manialink>