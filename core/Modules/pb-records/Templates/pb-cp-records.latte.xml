<?xml version="1.0" encoding="UTF-8" standalone="yes"?>
<manialink name="ESC:pb-cp-records" id="pb-cp-records" version="3">
    <framemodel id="cp_diff">
        <quad id="timeQuad" z-index="-2" pos="-4 0" size="15 5" bgcolor="0006" halign="right" valign="center"/>
        <label id="time" z-index="1" pos="-5 0" size="13 4" text="" textsize="1.5" halign="right" textcolor="ffff" valign="center" textfont="RajdhaniMono" textprefix="$s"/>

        <quad id="cpidQuad" z-index="-1" size="8 5" bgcolor="0009" halign="center"  valign="center"/>
        <label id="cpid" pos="0 0" text="" textsize="1.5" halign="center" textcolor="ffff" valign="center" textfont="RajdhaniMono" textprefix="$s"/>

        <quad id="diffQuad" z-index="-2" pos="4 0" size="15 5" bgcolor="f006" halign="left" valign="center"/>
        <label id="diff" z-index="1" pos="5 0" size="13 4" text="" textsize="1.5" halign="left" textcolor="ffff"  valign="center" textfont="RajdhaniMono" textprefix="$s"/>
    </framemodel>

    <frame pos="0 -60" scale="0.8">
        <label id="targetLabel" pos="0 -15" text="Loading..." textcolor="ffff" halign="center" textsize="0.5" textfont="RajdhaniMono" />

        <frame id="boxes">
            <frameinstance modelid="cp_diff" pos="0 5" scale="0.9" />
            <frameinstance modelid="cp_diff" pos="0 0" scale="0.8" />
            <frameinstance modelid="cp_diff" pos="0 -4" scale="0.6" />
            <frameinstance modelid="cp_diff" pos="0 -7.5" scale="0.6" />
            <frameinstance modelid="cp_diff" pos="0 -11" scale="0.6" />
            <frameinstance modelid="cp_diff" pos="0 -14.5" scale="0.6" />
        </frame>
    </frame>

    <script>
        <!--
    #Include "MathLib" as ML
    #Include "TextLib" as TL

        declare CMlFrame[] boxes;
        declare Integer[] cps;
        declare Text[] states;

        Text formatScoreCorrect(Integer scoreIn, Boolean cutZeroes){
            declare Text secondString;
            declare Text msString;
            declare Integer score;

            score = scoreIn;

            if(score < 0){
                score = -score;
            }

            declare Integer seconds = score / 1000;
            declare Integer minutes = seconds / 60;
            declare Integer ms = score - (seconds * 1000);

            seconds = seconds - (minutes * 60);

            secondString = "" ^ seconds;

            if(seconds < 10 && minutes > 0){
                secondString = "0" ^ seconds;
            }

            if(ms == 0){
                msString = "000";
            }else if(ms > 0 && ms < 10){
                msString = "00" ^ ms;
            }else if(ms >= 10 && ms < 100){
                msString = "0" ^ ms;
            }else{
                msString = "" ^ ms;
            }

            if(cutZeroes && minutes == 0){
                return secondString ^ "." ^ msString;
            }

            return minutes ^ ":" ^ secondString ^ "." ^ msString;
        }

        Integer diff(Integer record, Integer current){
            return current - record;
        }

        Void anim(Integer boxId, Text targetXml, Integer duration){
            AnimMgr.Add(boxes[boxId], targetXml, duration, CAnimManager::EAnimManagerEasing::QuintOut);
        }

        Void setBoxVisible(Integer boxId, Integer visibility){
            declare box = boxes[boxId];

            (box.GetFirstChild("timeQuad") as CMlQuad).Opacity = (0.7 * visibility);
            (box.GetFirstChild("diffQuad") as CMlQuad).Opacity = (0.7 * visibility);
            (box.GetFirstChild("cpidQuad") as CMlQuad).Opacity = (0.9 * visibility);
        }

        Void updateBox(Integer boxId, Integer cpId, Integer time){
            declare frame = boxes[boxId];

            //Set cp id
            if((cpId+1) == cps.count){
                (frame.GetFirstChild("cpid") as CMlLabel).SetText("ï„ž");
            }else{
                (frame.GetFirstChild("cpid") as CMlLabel).SetText("$s" ^ (cpId + 1));
            }

            //Set cp time
            (frame.GetFirstChild("time") as CMlLabel).SetText(formatScoreCorrect(time, True));

            if(cpId >= cps.count){
                return;
            }

            //Set difference and color
            declare diff = diff(cps[cpId], time);
            declare diffQuad = (frame.GetFirstChild("diffQuad") as CMlQuad);
            if(diff > 0){
                (frame.GetFirstChild("diff") as CMlLabel).SetText("+" ^ formatScoreCorrect(diff, True));
                diffQuad.BgColor = <0.5, 0.0, 0.0>;
            }else if(diff < 0){
                (frame.GetFirstChild("diff") as CMlLabel).SetText("-" ^ formatScoreCorrect(diff, True));
                diffQuad.BgColor = <0.0, 0.5, 0.0>;
            }else{
                (frame.GetFirstChild("diff") as CMlLabel).SetText("" ^ formatScoreCorrect(diff, True));
                diffQuad.BgColor = <0.0, 0.25, 0.5>;
            }

            setBoxVisible(boxId, 1);
        }

        Void transferBoxContents(Integer sourceBoxId, Integer targetBoxId){
            declare source <=> boxes[sourceBoxId];
            declare target <=> boxes[targetBoxId];

            declare Text cpid = (source.GetFirstChild("cpid") as CMlLabel).Value;

            if(cpid == ""){
                return;
            }

            (target.GetFirstChild("cpid") as CMlLabel).SetText(cpid);
            (target.GetFirstChild("time") as CMlLabel).SetText((source.GetFirstChild("time") as CMlLabel).Value);
            (target.GetFirstChild("diff") as CMlLabel).SetText((source.GetFirstChild("diff") as CMlLabel).Value);
            (target.GetFirstChild("diffQuad") as CMlQuad).BgColor = (source.GetFirstChild("diffQuad") as CMlQuad).BgColor;
            (target.GetFirstChild("diffQuad") as CMlQuad).Opacity = (source.GetFirstChild("diffQuad") as CMlQuad).Opacity;

            setBoxVisible(targetBoxId, 1);
        }

        Void eraseBoxContent(Integer boxId){
            declare box = boxes[boxId];

            setBoxVisible(boxId, 0);

            (box.GetFirstChild("cpid") as CMlLabel).SetText("");
            (box.GetFirstChild("time") as CMlLabel).SetText("");
            (box.GetFirstChild("diff") as CMlLabel).SetText("");
        }

        Void wayPoint(CTmRaceClientEvent event){
            declare id = event.CheckpointInRace;
            declare time = event.LapTime;

            updateBox(0, id, time);

            for(i, 0, 5){
                anim(i, states[i+1], 400);
            }

            sleep(400);

            for(i, 0, 5){
                anim(i, states[i], 1);
            }

            for(i, 0, 4){
                declare x = 5 - i;
                transferBoxContents(x-1, x);
            }
        }

        Void respawn(){
            if(InputPlayer != Null){
                if(InputPlayer.CurLap.NbRespawns == 0){
                    for(i, 0, 5){
                        eraseBoxContent(i);
                    }
                }
            }
        }

        main(){
            declare CMlFrame boxesFrame = (Page.MainFrame.GetFirstChild("boxes") as CMlFrame);
            declare targetLabel = (Page.MainFrame.GetFirstChild("targetLabel") as CMlLabel);

            declare Text PbCheckpoints for LocalUser;
            declare Text PbTargetString for LocalUser;
            declare Text lastPbCps = "";

            states = [
                """<frameinstance modelid="cp_diff" pos="0 5" scale="0.9" />""",
                """<frameinstance modelid="cp_diff" pos="0 0" scale="0.8" />""",
                """<frameinstance modelid="cp_diff" pos="0 -4" scale="0.6" />""",
                """<frameinstance modelid="cp_diff" pos="0 -7.5" scale="0.6" />""",
                """<frameinstance modelid="cp_diff" pos="0 -11" scale="0.6" />""",
                """<frameinstance modelid="cp_diff" pos="0 -14.5" scale="0.6" />""",
                """<frameinstance modelid="cp_diff" pos="0 -18" scale="0.6" />"""
            ];

            foreach(control in boxesFrame.Controls){
                boxes.add((control as CMlFrame));
            }

            declare Boolean targetHidden = False;
            declare Text lastHash = "";

            boxesFrame.ClipWindowActive = True;
            boxesFrame.ClipWindowRelativePosition = <0.0,-5.0>;
            boxesFrame.ClipWindowSize = <50.0,15.0>;

            respawn();

            while(True){
                yield;

                wait(InputPlayer != Null);

                if(lastPbCps != PbCheckpoints){
                    lastPbCps = PbCheckpoints;

                    if(PbCheckpoints == "-1"){
                        boxesFrame.Hide();
                        targetLabel.Hide();
                    }else{
                        boxesFrame.Show();
                        targetLabel.Show();
                    }

                    declare cpTimes = TL::Split(",", PbCheckpoints);
                    cps = Integer[];
                    foreach(cp in cpTimes){
                        cps.add(TL::ToInteger(cp));
                    }
                    targetLabel.SetText(PbTargetString ^ "  " ^ formatScoreCorrect(cps[cps.count - 1], True));
                    respawn();
                }

                if(cps.count > 0){
                    foreach(Event in RaceEvents){
                        if(Event.Player.User.Login == InputPlayer.User.Login){
                            switch(Event.Type){
                                case CTmRaceClientEvent::EType::WayPoint: wayPoint(Event);
                                case CTmRaceClientEvent::EType::Respawn: respawn();
                            }
                        }
                    }
                }

                declare speed = InputPlayer.Speed;

                if(speed < 0){
                    speed = -speed;
                }

                if(speed > 10.0 && !targetHidden){
                    AnimMgr.Add(targetLabel, """<label textcolor="fff0" />""", 800, CAnimManager::EAnimManagerEasing::QuadOut);
                    targetHidden = True;
                }
                if(speed < 10.0 && targetHidden){
                    AnimMgr.Add(targetLabel, """<label textcolor="ffff" />""", 500, CAnimManager::EAnimManagerEasing::QuadIn);
                    targetHidden = False;
                }
            }
        }
        -->
    </script>
</manialink>