<?xml version="1.0" encoding="UTF-8" standalone="yes"?>
<manialink name="ESC:paypal-widget" id="paypal-widget" version="3">
    <frame pos="{config('paypal.pos.visible')|noescape}" id="widget">
        <quad id="background" pos="0 0" size="8 10" bgcolor="{config('colors.ui.background')}" opacity="0.6" z-index="-1" ScriptEvents="1" />

        <frame z-index="2">
            <quad pos="4 -4" size="4 4" halign="center" valign="center" image="https://i.imgur.com/Lm0arwo.png" imagefocus="" />

            <label id="time" pos="4 -8.2" size="6 1" valign="center" halign="center" textsize="0.002" text="Donate" textfont="RajdhaniMono" />
        </frame>
    </frame>

    <script><!--
        #Include "TextLib" as TextLib

        Void anim(CMlFrame frame, Text targetPos, Integer duration){
            AnimMgr.Add(frame, targetPos, duration, CAnimManager::EAnimManagerEasing::ExpInOut);
        }

        main() {
            declare Text visiblePos = "<frame pos='{config('paypal.pos.visible')|noescape}' />";
            declare Text hiddenPos = "<frame pos='{config('paypal.pos.hidden')|noescape}' />";
            declare widget <=> (Page.MainFrame.GetFirstChild("widget") as CMlFrame);
            declare background <=> (Page.MainFrame.GetFirstChild("background") as CMlQuad);
            declare Boolean hidden = False;
            declare Boolean isLapRace = Map.TMObjective_IsLapRace;
            declare Integer duration = 800;
            declare Real speed = 0.0;
            declare hoverColor = <0.078, 0.729, 0.729>;

            while(True){
                yield;

                foreach(event in PendingEvents){
                    if(event.Control == background && event.Type == CMlScriptEvent::Type::MouseClick){
                        TriggerPageAction("rickroll");
                        OpenLink("{config('paypal.url')|noescape}", ::LinkType::ExternalBrowser);
                    }
                    if(event.Control == background && event.Type == CMlScriptEvent::Type::MouseOver){
                        AnimMgr.Add(background, """<quad opacity="0.95" bgcolor="038" />""", 100, CAnimManager::EAnimManagerEasing::Linear);
                    }
                    if(event.Control == background && event.Type == CMlScriptEvent::Type::MouseOut){
                        AnimMgr.Add(background, """<quad opacity="0.75" bgcolor="{config('colors.ui.background')|noescape}" />""", 100, CAnimManager::EAnimManagerEasing::Linear);
                    }
                }

                if(InputPlayer != Null){
                    declare Real UI_Hide_Speed for InputPlayer = 1.0;

                    speed = InputPlayer.Speed;

                    if(speed < 0){
                        speed = speed  * -1;
                    }

                    if(!isLapRace && InputPlayer.CurrentNbLaps >= 1 || UI_Hide_Speed == 0.0){
                        if(speed < UI_Hide_Speed && hidden){
                            anim(widget, visiblePos, duration);
                            hidden = False;
                        }
                    }else{
                        if(speed >= UI_Hide_Speed && !hidden){
                            anim(widget, hiddenPos, duration);
                            hidden = True;
                        }

                        if(speed < UI_Hide_Speed && hidden){
                            anim(widget, visiblePos, duration);
                            hidden = False;
                        }
                    }
                }
            }
        }
    --></script>
</manialink>